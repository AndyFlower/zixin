# 字节码文件

## 字节码文件解析

```java
package com.zixin.bytecode;

/**
 * @ClassName Test1
 * @Description TODO
 * @Author zixin
 * @Date 2020/1/20 10:48
 * @Version 1.0
 **/
public class Test1 {
    private int a=1;

    public int getA() {
        return a;
    }

    public void setA(int a) {
        this.a = a;
    }
}

```

`javap -c Test1`

```java
Compiled from "Test1.java"
public class com.zixin.bytecode.Test1 {
  public com.zixin.bytecode.Test1();
    Code:
       0: aload_0
       1: invokespecial #1                  // Method java/lang/Object."<init>":()V
       4: aload_0
       5: iconst_1
       6: putfield      #2                  // Field a:I
       9: return

  public int getA();
    Code:
       0: aload_0
       1: getfield      #2                  // Field a:I
       4: ireturn

  public void setA(int);
    Code:
       0: aload_0
       1: iload_1
       2: putfield      #2                  // Field a:I
       5: return
}

```

`javap -verbose Test1`

```java
Classfile /D:/slpworkspace/github/2020/project/jvm/jvm-lecture/target/classes/com/zixin/by
tecode/Test1.class
  Last modified 2020-1-20; size 477 bytes
  MD5 checksum 9bdf19f17a723991eb5f1f25ad0cdb35
  Compiled from "Test1.java"
public class com.zixin.bytecode.Test1
  minor version: 0  # 小版本
  major version: 50  # 大版本
  flags: ACC_PUBLIC, ACC_SUPER
Constant pool:    # 常量池
   #1 = Methodref          #4.#20         // java/lang/Object."<init>":()V
   #2 = Fieldref           #3.#21         // com/zixin/bytecode/Test1.a:I
   #3 = Class              #22            // com/zixin/bytecode/Test1
   #4 = Class              #23            // java/lang/Object
   #5 = Utf8               a
   #6 = Utf8               I
   #7 = Utf8               <init>     //构造方法
   #8 = Utf8               ()V        //无参数的构造方法 返回为void
   #9 = Utf8               Code
  #10 = Utf8               LineNumberTable
  #11 = Utf8               LocalVariableTable
  #12 = Utf8               this
  #13 = Utf8               Lcom/zixin/bytecode/Test1;
  #14 = Utf8               getA
  #15 = Utf8               ()I
  #16 = Utf8               setA
  #17 = Utf8               (I)V
  #18 = Utf8               SourceFile
  #19 = Utf8               Test1.java
  #20 = NameAndType        #7:#8          // "<init>":()V
  #21 = NameAndType        #5:#6          // a:I
  #22 = Utf8               com/zixin/bytecode/Test1
  #23 = Utf8               java/lang/Object
{
  public com.zixin.bytecode.Test1();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=2, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: aload_0
         5: iconst_1
         6: putfield      #2                  // Field a:I
         9: return
      LineNumberTable: #行号表
        line 10: 0
        line 11: 4
      LocalVariableTable: # 局部变量表
        Start  Length  Slot  Name   Signature
            0      10     0  this   Lcom/zixin/bytecode/Test1;

  public int getA();
    descriptor: ()I
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: getfield      #2                  // Field a:I
         4: ireturn
      LineNumberTable:#行号表
        line 14: 0
      LocalVariableTable: # 局部变量表
        Start  Length  Slot  Name   Signature
            0       5     0  this   Lcom/zixin/bytecode/Test1;

  public void setA(int);
    descriptor: (I)V
    flags: ACC_PUBLIC
    Code:
      stack=2, locals=2, args_size=2
         0: aload_0
         1: iload_1
         2: putfield      #2                  // Field a:I
         5: return
      LineNumberTable:    #行号表
        line 18: 0
        line 19: 5
      LocalVariableTable:    # 局部变量表
        Start  Length  Slot  Name   Signature
            0       6     0  this   Lcom/zixin/bytecode/Test1;
            0       6     1     a   I
}
SourceFile: "Test1.java"

```

```JAVA
cafe babe(魔数) 0000(小版本号) 0032(主版本号) 0018(常量池数量) 0a(第一个常量的标志位10CONSTANT_Methodref_info)00 04(索引为4)00 14(索引为20)# #1 = Methodref          #4.#20 # 09(CONSTANT_Fieldref_info)
0003(index 3) 0015(index 21) 07(7 CONSTANT_Class_Info)00 16(22  com/zixin/bytecode/Test1
)07(7 CONSTANT_Class_Info) 0017(23 java/lang/Object
) 01(1 CONSTANT_Utf8_info)00 01(字节长度)61(a) 01(1 CONSTANT_Utf8_info)00
01(字节长度)49(75 -i) 01(1 CONSTANT_Utf8_info)00 06(6个字节)3c 696e 6974 3e（<init>）01(1 CONSTANT_Utf8_info) 0003(字节长度) 2829
56(()V)01(1 CONSTANT_Utf8_info) 0004(字节长度) 436f 6465(Code) 01(1 CONSTANT_Utf8_info)00 0f(字节长度15)4c 696e 654e
756d 6265 7254 6162 6c65(LineNumberTable) 01(1 CONSTANT_Utf8_info)00 12(自己长度18位)4c 6f63
616c 5661 7269 6162 6c65 5461 626c 65(LocalVariableTable)01(1 CONSTANT_Utf8_info)
0004(字节长度4) 7468 6973（this） 01(1 CONSTANT_Utf8_info)00 1a(字节长度26)4c 636f 6d2f 7a69
7869 6e2f 6279 7465 636f 6465 2f54 6573
7431 3b( Lcom/zixin/bytecode/Test1;)01(1 CONSTANT_Utf8_info) 0004(字节长度4) 6765 7441(getA) 01(1 CONSTANT_Utf8_info)00 03(字节长度3)28 2949(()I 不接收参数  返回一个整型值)
01(1 CONSTANT_Utf8_info)00 04(字节长度4)73 6574 41(setA)01(1 CONSTANT_Utf8_info) 0004(字节长度4) 2849 2956((I)V) 01(1 CONSTANT_Utf8_info)00
0a(字节长度10)53 6f75 7263 6546 696c 65(SourceFile)01(1 CONSTANT_Utf8_info) 000a(字节长度10) 5465
7374 312e 6a61 7661(Test1.java) 0c(12 CONSTANT_NameAndType_info)00 07(index 7)00 08(index 8)0c(12 CONSTANT_NameAndType_info) 0005(index 5)
0006(index 6) 01(1 CONSTANT_Utf8_info)00 18(字节长度24)63 6f6d 2f7a 6978 696e 2f62
7974 6563 6f64 652f 5465 7374 31(com/zixin/bytecode/Test1)01(1 CONSTANT_Utf8_info) 0010(字节长度16)
6a61 7661 2f6c 616e 672f 4f62 6a65 6374(java/lang/Object)----常量池结束
0021 0003 0004 0000 0001 0002 0005 0006
0000 0003 0001 0007 0008 0001 0009 0000
0038 0002 0001 0000 000a 2ab7 0001 2a04
b500 02b1 0000 0002 000a 0000 000a 0002
0000 000a 0004 000b 000b 0000 000c 0001
0000 000a 000c 000d 0000 0001 000e 000f
0001 0009 0000 002f 0001 0001 0000 0005
2ab4 0002 ac00 0000 0200 0a00 0000 0600
0100 0000 0e00 0b00 0000 0c00 0100 0000
0500 0c00 0d00 0000 0100 1000 1100 0100
0900 0000 3e00 0200 0200 0000 062a 1bb5
0002 b100 0000 0200 0a00 0000 0a00 0200
0000 1200 0500 1300 0b00 0000 1600 0200
0000 0600 0c00 0d00 0000 0000 0600 0500
0600 0100 0100 1200 0000 0200 13
```

## Java字节码整体结构

| 4个字节   | Magic Number               |
| --------- | -------------------------- |
| 2+2个字节 | Version                    |
| 2+n个字节 | Constant Pool              |
| 2个字节   | Access Flags 访问标识      |
| 2个字节   | This Class Name 当前类名字 |
| 2个字节   | Super Class Name 父类      |
| 2+n个字节 | Interfaces  接口           |
| 2+n个字节 | Fields  当前类成员变量     |
| 2+n个字节 | Methods  方法              |
| 2+n个字节 | Attributes  属性           |

| 类型           | 名称                             | 数量                  |
| -------------- | -------------------------------- | --------------------- |
| u4             | magic(魔数)                      | 1                     |
| u2             | minor_version(次版本号)          | 1                     |
| u2             | major_version(主版本号)          | 1                     |
| u2             | constant_pool_count(常量个数)    | 1                     |
| cp_info        | constant_pool(常量池表)          | constant_pool_count-1 |
| u2             | access_flags(类的访问控制权限)   | 1                     |
| u2             | this_class(类名)                 | 1                     |
| u2             | super_class(父类名)              | 1                     |
| u2             | interfaces_count(接口个数)       | 1                     |
| u2             | interfaces(接口名)               | interfaces_count      |
| u2             | fields_count(域个数)             | 1                     |
| field_info     | fields(域的表)                   | fields_count          |
| u2             | methods_count(方法的个数)        | 1                     |
| method_info    | methods(方法表)                  | method_count          |
| u2             | attributes_count(附加属性的个数) | 1                     |
| attribute_info | attributes(附件属性的表)         | attributes_count      |

## Class字节码中有两种数据类型

1. 字节数据直接量

   这是基本的数据类型。共细分为u1 u2 u4 u8四种，分别代表连续的1个字节 2个字节 4个字节 8个字节组成的整体数据。

2. 表（数组）

   表是由多个基本数据或其他表，按照既定顺序组成的大的数据集合，表是有结构的，他的结构体现在：组成表的成分所在的位置和顺序都是已经严格定义好的。

## Access_Flag访问标志

访问标志信息包括该Class文件是类还是接口，是否被定义成public，

### 一、使用javap -verbose命令分析一个字节码文件时，将会分析该字节码文件的魔数、版本号、常量池、类信息、类的构造方法、类中的方法信息、类变量与成员变量等信息。

1. 魔数：所有的`.class`字节码文件的前4个字节都是魔数，魔数值为固定值：`0XCAFEBABE`

2. 魔数之后的4个字节是版本号，前两个是`minor version `后两个是`major version`(50对应jdk6)

3. 常量池（`constant pool`）：版本号之后的就是常量池入口，一个java类定义的很多信息都是由常量池来维护和描述的，我能可以将常量池看做是class的资源仓库，比如说java类中定义的方法与变量信息都是存储在常量池中，常量池中主要存储两类常量：字面量与符号引用。字面量如文本字符串，java中声明为final的常量值等，符号引用如类和接口的全局限定名，方法的名称和描述符等。

4. 常量池的总体结构：Java类锁对应的常量池主要由常量池数量与常量池数组这两部分共同构成。常量池数量紧跟在主版本号后面占据两个字节，而常量池数组跟在常量池数量之后，常量池数组与一般的数组不同的是，常量池数组中，不同的元素的类型结果都是不同的,长度当然也就不同。但是，每一种元素的第一个数据都是`u1`类型，该字节是个标志位，占1个字节。`jvm`解析常量值时，根据`u1`类型来获取元素的具体类型。常量池中数组元素的个数等于常量值数减1。目的是满足某些常量池索引的数据在特定情况下不引用任何一个常量池的含义。根本原因在于：索引为0也是一个常量，是jvm的保留常量，不位于常量表中，这个常量值就是null,所以，常量池的索引从1开始而不是0.

5. 在JVM规范中，每个变量/字段都有描述信息，描述信息的主要作用是描述字段的数据类型、方法的参数列表（包括数量、类型与顺序）与返回值。根据描述符规则，基本数据类型和代表无返回值的void类型都用一个大写字符来表示，对象类型则使用字符L加对象的全限定名称来表示。为了压缩字节码文件的体积，对于基本数据类型，JVM只使用一个大写字母来表示，如下所示：`B-byte`，`C-char`，`D-double` ，`F-float`， `I-int`，` J-long `，`S-short` ，`Z-boolean` ，`V-void`， L-对象类型，如`Ljava/lang/String;`

6. 对于数组类型来说，每一个维度使用一个前置的[来表示，如`int[]` 被记为[I,`String [][]`[][]被记录为`[[Ljava/lang/String`

7. 用描述符描述方法时，按照先参数列表，后返回值的顺序来描述。参数列表按照参数的严格顺序放在一组()之内，如String getRealNameByInameAndNickName(int id,String name)的描述符为：(I,Ljava/lang/string)Ljava/lang/string

   | 常量                             | 项目   | 类型 | 描述                                                      |
   | -------------------------------- | ------ | ---- | --------------------------------------------------------- |
   | CONSTANT_Utf8_info               | tag    | U1   | 值为1                                                     |
   | CONSTANT_Utf8_info               | length | U2   | UTF-8编码的字符串长度                                     |
   | CONSTANT_Utf8_info               | bytes  | U1   | 长度为length的UTF-8编码的字符串                           |
   | CONSTANT_Integer_info            | tag    | U1   | 值为3                                                     |
   | CONSTANT_Integer_info            | bytes  | U4   | 按照高位在前存储的int值                                   |
   | CONSTANT_Float_info              | tag    | U1   | 值为4                                                     |
   | CONSTANT_Float_info              | bytes  | U4   | 按照高位在前存储的float值                                 |
   | CONSTANT_Long_info               | tag    | U1   | 值为5                                                     |
   | CONSTANT_Long_info               | bytes  | U8   | 按照高位在前的long值                                      |
   | CONSTANT_Double_Info             | tag    | U1   | 值为6                                                     |
   | CONSTANT_Double_Info             | bytes  | U8   | 按照高位在前存储的double值                                |
   | CONSTANT_Class_Info              | tag    | U1   | 值为7                                                     |
   | CONSTANT_Class_Info              | index  | U2   | 指向全限定名常量项的索引                                  |
   | CONSTANT_String_info             | tag    | U1   | 值为8                                                     |
   | CONSTANT_String_info             | index  | U2   | 指向字符串字面量的索引                                    |
   | CONSTANT_Fieldref_info           | tag    | U1   | 值为9                                                     |
   | CONSTANT_Fieldref_info           | index  | U2   | 指向声明字段的类或者接口描述符CONSTANT_Class_info的索引项 |
   | CONSTANT_Fieldref_info           | index  | U2   | 指向字段描述符CONSTANT_NameAndType_info的索引项           |
   | CONSTANT_Methodref_info          | tag    | U1   | 值为10                                                    |
   | CONSTANT_Methodref_info          | index  | U2   | 指向声明方法的类描述符CONSTANT_Class_info的索引项         |
   | CONSTANT_Methodref_info          | index  | U2   | 指向字段描述符CONSTANT_NameAndType_info的索引项           |
   | CONSTANT_InterfaceMethodref_info | tag    | U1   | 值为11                                                    |
   | CONSTANT_InterfaceMethodref_info | index  | U2   | 指向声明方法的接口描述符CONSTANT_Class_info的索引项       |
   | CONSTANT_InterfaceMethodref_info | index  | U2   | 指向名称及类型描述符CONSTANT_NameAndType_info的索引项     |
   | CONSTANT_NameAndType_info        | tag    | U1   | 值为12                                                    |
   | CONSTANT_NameAndType_info        | index  | U2   | 指向该字段或方法名称常量项的索引                          |
   | CONSTANT_NameAndType_info        | index  | U2   | 指向该字段或方法描述符常量项的索引                        |

   在jdk1.7之后又增加了3种：（CONSTANT_MethodHandle_info,CONSTANT_MethodType_info以及CONSTANT_InvokeDynamic_info）一共14种

   ### 二、synchronized关键字字节码文件分析

   ```java
   private void test(String str){
           /*此处如果使用str起不到上锁的作用 要用同一个对象 所以类中定义一个变量来上锁*/
           synchronized (object){
               System.out.println("hello world");
           }
       }
   ```

   

   ```java
    private void test(java.lang.String);
       descriptor: (Ljava/lang/String;)V
       flags: ACC_PRIVATE
       Code:
         stack=2, locals=4, args_size=2
            0: aload_0
            1: getfield      #6                  // Field object:Ljava/lang/Object;
            4: dup
            5: astore_2
            6: monitorenter     #进入synchronized代码块
            7: getstatic     #12                 // Field java/lang/System.out:Ljava/io/PrintStream;
           10: ldc           #13                 // String hello world
           12: invokevirtual #14                 // Method java/io/PrintStream.println:(Ljava/lang/String;)V
           15: aload_2
           16: monitorexit
           17: goto          25
           20: astore_3
           21: aload_2
           22: monitorexit   # 异常退出
           23: aload_3
           24: athrow
           25: return
         Exception table:
            from    to  target type
                7    17    20   any
               20    23    20   any
         LineNumberTable:
           line 22: 0
           line 23: 7
           line 24: 15
           line 25: 25
         LocalVariableTable:
           Start  Length  Slot  Name   Signature
               0      26     0  this   Lcom/zixin/bytecode/Test2;
               0      26     1   str   Ljava/lang/String;
         StackMapTable: number_of_entries = 2
           frame_type = 255 /* full_frame */
             offset_delta = 20
             locals = [ class com/zixin/bytecode/Test2, class java/lang/String, class java/lang/Object ]
             stack = [ class java/lang/Throwable ]
           frame_type = 250 /* chop */
             offset_delta = 4
   
   
   ```

   

   

